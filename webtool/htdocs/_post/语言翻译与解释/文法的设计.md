# 文法的设计
> 2018-10-10 22:18
> 文法 解释器 star

> &copy;Power By [ojisuan.com(o计算)](http://www.ojisuan.com"http://www.ojisuan.com") 作者: [Ooo]("qq:978831608")

------

以一个表达式为例，该表达式支持形式如下：

- 1 - (2 * 3) < 4 == false
- "hello" + "body!" == true

表达式包含四种基本表达式：

-  **Literals**:Numbers, strings, Booleans, and nil.//数字，字符串，布尔值，空值
-  **Unary expressions**:前缀运算符！表示逻辑非，-表示对一个数值求反
-  **Binary expressions**:+, -, *, /,==, !=, <, <=, >, >=.常见的比较运算符
-  **Parentheses for grouping**：（）表达式

写出该表达式的上下文无关文法，此时还是模糊文法，后面将模糊文法一步步转换为支持递归向下解析的确定文法

## 归纳出模糊文法
该表达式的模糊文法如下：

```
expression → literal
           | unary
           | binary
           | grouping ;

literal    → NUMBER | STRING | "true" | "false" | "nil" ;
grouping   → "(" expression ")" ;
unary      → ( "-" | "!" ) expression ;
binary     → expression operator expression ;
operator   → "==" | "!=" | "<" | "<=" | ">" | ">="
           | "+"  | "-"  | "*" | "/" ;
```

## 确定优先级和结合性

归纳出表达式中的一元操作符，二元操作符......，并设计操作符的优先级和结合性，列出相关表格。

- Precedence ：higher precedence operators， precedence operators, higher precedence operators are said to “bind tighter”.
- Associativity :  left-associative (“left-to-right”),  right-associative (“right-to-left”).

| Name | Operators | Associates |
|  :------|    :------    |    :------     |
|          Unary	        |        ! -            	|         Right       |
| Multiplication    	|        /  *              |          Left        |
| Addition             |	- +                   |        Left            |
|  Comparison	    | > >= < <=         |	      Left            |
|  Equality           	|  == !=	            |        Left            |

## 生成确定文法

用新的非终结符Unary，Multiplication，Addition，Comparison，Equality运用优先级和结合性对模糊文法进行改写，生成的文法按照运算符优先级从低到高的顺序从上往下排列，头文法规则（expression）只能出现在基础文法规则中，最简形式规则如果还包含expression，则将此规则归纳到基础文法规则中（如本例子中的grouping）。

单条非终结符的改写，以Multiplication为例，用高于Multiplication优先级的非终结符替代binary中的expression

`binary → expression operator expression ;`

改写为：

`multiplication → higherThanMultiply "*" higherThanMultiply ;`

higherThanMultiply表示比multiplication高一级优先级的非终结符，从表中可知为unary，用unary替代higherThanMultiply为：

`multiplication → unary ( "*" | "/" ) unary ;`

根据multiplication的左结合性将左边的unary替换为multiplication,同理如果为右结合性将右边替换掉:

`multiplication → multiplication ( "*" | "/" ) unary ;`

此时出现left-recursive左递归文法，需要消除左递归，消除后为:

`multiplication → unary ( ( "/" | "*" ) unary )* ;`

unary改写的简要说明：

```
unary          → ( "-" | "!" ) expression ;
unary          → ( "-" | "!" ) higherThanUnary;
unary          → ( "!" | "-" ) primary;//右结合，下个步骤用unary替换primary
unary          → ( "!" | "-" ) unary;
//因为后一条文法即为基础文法，为了构造递归向下解析，所以unary也可以是primary构成
unary          → ( "!" | "-" ) unary
               | primary ;
expression只能出现在基础文法规则primary中,所以grouping   → "(" expression ")" ;规则直接归到primary下：
primary        → NUMBER | STRING | "false" | "true" | "nil"
               | "(" expression ")" ;
```

对每个新非终结符进行类似操作最终得到(注意：生成的文法按照运算符优先级从低到高的顺序)：

```
expression     → equality ;
equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → addition ( ( ">" | ">=" | "<" | "<=" ) addition )* ;
addition       → multiplication ( ( "-" | "+" ) multiplication )* ;
multiplication → unary ( ( "/" | "*" ) unary )* ;
unary          → ( "!" | "-" ) unary
               | primary ;
primary        → NUMBER | STRING | "false" | "true" | "nil"
               | "(" expression ")" ;
```

> * 作者：Ooo  -- QQ:`978831608`
> * 邮箱：<978831608@qq.com>
> * 链接：[o计算](http://www.ojisuan.com"http://www.ojisuan.com")
> * 转载请注明出处。


